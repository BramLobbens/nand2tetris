// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs,
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    /* 1. if (zx == 1) set x = 0        // 16-bit constant
    */
    // if zx = 1 -> zx16 = 0000000000000000, else 1111111111111111
    Mux16(a=true, b=false, sel=zx, out=zx16);
    // And with 0 -> set all bits to zero; And with 1 -> keep input
    And16(a=zx16, b=x, out=zxout);

    /* 2. if (nx == 1) set x = !x       // bitwise not
    */
    // select zxout or negated zxout as output
    Not16(in=zxout, out=notx);
    Mux16(a=zxout, b=notx, sel=nx, out=nxout);

    /* 3. if (zy == 1) set y = 0        // 16-bit constant
    */
    // if zy = 1 -> zy16 = 0000000000000000, else 1111111111111111
    Mux16(a=true, b=false, sel=zy, out=zy16);
    // And with 0 -> set all bits to zero; And with 1 -> keep input
    And16(a=zy16, b=y, out=zyout);

    /* 4. if (ny == 1) set y = !y       // bitwise not
    */
    // select zyout or negated zyout as output
    Not16(in=zyout, out=noty);
    Mux16(a=zyout, b=noty, sel=ny, out=nyout);

    /* 5. if (f == 1)  set out = x + y  // integer 2's complement addition
          if (f == 0)  set out = x & y  // bitwise and
    */
    // x & y
    And16(a=nxout, b=nyout, out=andout);
    // x + y
    Add16(a=nxout, b=nyout, out=addout);

    Mux16(a=andout, b=addout, sel=f, out=fout);

    /* 6. if (no == 1) set out = !out   // bitwise not
          if (out == 0) set zr = 1
          if (out < 0) set ng = 1
    */
    // set output
    Not16(in=fout, out=notfout);
    Mux16(a=fout, b=notfout, sel=no, out[0..7]=lowend, out[8..15]=bigend, out=out, out[15]=ng);

    // Check for zero
    Or8Way(in=lowend, out=w1);
    Or8Way(in=bigend, out=w2);
    Or(a=w1, b=w2, out=w3);
    Not(in=w3, out=zr);
}